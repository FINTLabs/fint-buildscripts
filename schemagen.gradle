def generatedDir = "${project.buildDir}/generated-schema"
def resourcesDir = 'src/main/resources/schema'

configurations {
    xjc
}

dependencies {
    xjc 'com.sun.xml.bind:jaxb-xjc:2.2.7-b41'
    xjc 'com.sun.xml.bind:jaxb-impl:2.2.7-b41'
    xjc 'javax.xml.bind:jaxb-api:2.2.7'
}

task createDirs << {
    file(generatedDir).mkdirs()
    file(resourcesDir).mkdirs()
}

task schemagen << {
    ant.taskdef(name: 'schemagen', classname: 'com.sun.tools.jxc.SchemaGenTask', classpath: configurations.xjc.asPath)
    ant.schemagen(
            srcdir: project.ext.schemagenModelDir,
            destdir: generatedDir,
            includeAntRuntime: false
    )
}

task postSchemagen(type: Copy) {
    from generatedDir
    into resourcesDir
    rename 'schema1.xsd', project.ext.schemagenXsd
}

schemagen.dependsOn createDirs
schemagen.finalizedBy postSchemagen
