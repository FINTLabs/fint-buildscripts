project.ext {
    jaxbVersion = '2.2.11'
    generatedSourcesDir = 'src/main/java'
    xsdResourcesDir = 'src/main/resources/schema'
}

configurations {
    xjc
}

dependencies {
    compile "com.sun.xml.bind:jaxb-xjc:${jaxbVersion}"
    compile "com.sun.xml.bind:jaxb-core:${jaxbVersion}"
    compile "com.sun.xml.bind:jaxb-impl:${jaxbVersion}"
    compile "javax.xml.bind:jaxb-api:${jaxbVersion}"

    xjc 'com.github.jaxb-xew-plugin:jaxb-xew-plugin:1.8'
    xjc 'net.java.dev.jaxb2-commons:jaxb-fluent-api:2.1.8'
}

task downloadXsd << {
    new File(project.xsdResourcesDir).mkdirs()
    project.ext.xsdUrls.each { url ->
        def fileName = new File(url).getName()
        def common = new File("${project.xsdResourcesDir}/${fileName}")
        new URL(url).withInputStream { i ->
            common.withOutputStream {
                it << i
            }
        }
    }
}

task processXsd << {
    System.setProperty('javax.xml.accessExternalSchema', 'all')
    ant.taskdef(name: 'xjc', classname: 'com.sun.tools.xjc.XJCTask', classpath: configurations.compile.asPath)

    mkdir project.generatedSourcesDir
    ant.xjc(destdir: project.generatedSourcesDir, extension: true) {
        classpath {
            pathelement(path: configurations.xjc.asPath)
        }
        schema(dir: project.xsdResourcesDir, includes: '*.xsd')
        arg(value: '-disableXmlSecurity')
        arg(value: '-b')
        arg(value: 'jaxb-bindings.xml')
    }
}
